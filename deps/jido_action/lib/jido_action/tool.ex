defmodule Jido.Action.Tool do
  @moduledoc """
  Provides functionality to convert Jido Actions into generic tool representations.

  This module allows Jido Actions to be converted into standardized tool maps
  that can be used by various AI integration layers.

  ## Tool Formats

  - `to_tool/1` - Returns a generic tool map with name, description, function, and schema

  ## Utility Functions

  - `convert_params_using_schema/2` - Normalizes LLM arguments (string keys â†’ atom keys, type coercion)
  - `build_parameters_schema/1` - Converts action schema to JSON Schema format
  - `execute_action/3` - Executes an action with schema-based param conversion
  """

  alias Jido.Action.Schema

  @type tool :: %{
          name: String.t(),
          description: String.t(),
          function: (map(), map() -> {:ok, String.t()} | {:error, String.t()}),
          parameters_schema: map()
        }

  @doc """
  Converts a Jido Exec into a tool representation.

  ## Arguments

    * `action` - The module implementing the Jido.Action behavior.

  ## Returns

    A map representing the action as a tool, compatible with systems like LangChain.

  ## Examples

      iex> tool = Jido.Action.Tool.to_tool(MyExec)
      %{
        name: "my_action",
        description: "Performs a specific task",
        function: #Function<...>,
        parameters_schema: %{...}
      }
  """
  @spec to_tool(module()) :: tool()
  def to_tool(action) when is_atom(action) do
    %{
      name: action.name(),
      description: action.description(),
      function: &execute_action(action, &1, &2),
      parameters_schema: build_parameters_schema(action.schema())
    }
  end

  @doc """
  Executes an action and formats the result for tool output.

  This function is typically used as the function value in the tool representation.
  """
  @spec execute_action(module(), map(), map()) :: {:ok, String.t()} | {:error, String.t()}
  def execute_action(action, params, context) do
    # Convert string keys to atom keys and handle type conversion based on schema
    converted_params = convert_params_using_schema(params, action.schema())
    safe_context = context || %{}

    case Jido.Exec.run(action, converted_params, safe_context) do
      {:ok, result} ->
        {:ok, Jason.encode!(result)}

      {:error, %_{} = error} when is_exception(error) ->
        {:error, Jason.encode!(%{error: inspect(error)})}

      {:error, reason} ->
        {:error, Jason.encode!(%{error: inspect(reason)})}
    end
  end

  @doc """
  Helper function to convert params using schema information.

  Converts string keys to atom keys and handles type conversion based on schema.
  Supports both atom and string input keys, and preserves unknown keys (open validation).
  """
  def convert_params_using_schema(params, schema) when is_map(params) do
    schema_keys = Schema.known_keys(schema)

    {known_converted, unknown_params} =
      Enum.reduce(schema_keys, {%{}, params}, fn key, {known_acc, rest} ->
        string_key = to_string(key)
        {val_atom, rest} = Map.pop(rest, key, :__missing__)

        {value, rest} =
          case val_atom do
            :__missing__ ->
              Map.pop(rest, string_key, :__missing__)

            _ ->
              {_val_string, rest2} = Map.pop(rest, string_key, :__missing__)
              {val_atom, rest2}
          end

        case value do
          :__missing__ ->
            {known_acc, rest}

          _ ->
            converted_value = convert_value_with_schema(schema, key, value)
            {Map.put(known_acc, key, converted_value), rest}
        end
      end)

    Map.merge(unknown_params, known_converted)
  end

  defp convert_value_with_schema(schema, key, value) when is_list(schema) do
    schema_entry = Keyword.get(schema, key, [])
    type = Keyword.get(schema_entry, :type)
    coerce_value(type, value)
  end

  defp convert_value_with_schema(_schema, _key, value) do
    # For Zoi schemas, let the validation handle conversion
    value
  end

  defp coerce_value(:float, value) when is_binary(value) do
    parse_float(value)
  end

  defp coerce_value(:float, value) when is_integer(value) do
    value * 1.0
  end

  defp coerce_value(:integer, value) when is_binary(value) do
    parse_integer(value)
  end

  defp coerce_value(_type, value), do: value

  defp parse_float(value) do
    case Float.parse(value) do
      {num, _} -> num
      :error -> value
    end
  end

  defp parse_integer(value) do
    case Integer.parse(value) do
      {num, _} -> num
      :error -> value
    end
  end

  @doc """
  Builds a parameters schema for the tool based on the action's schema.

  ## Arguments

    * `schema` - The NimbleOptions or Zoi schema from the action.

  ## Returns

    A map representing the parameters schema in a format compatible with LangChain.
  """
  @spec build_parameters_schema(Schema.t()) :: map()
  def build_parameters_schema(schema) do
    Schema.to_json_schema(schema)
  end
end
